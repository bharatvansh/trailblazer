// This module is a pure Java library. It has no dependencies on Minecraft, Spigot, or Fabric.
// This ensures that both the plugin and the mod can use its classes.

plugins {
	id 'java'
	id 'jacoco'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation platform('org.junit:junit-bom:5.10.2')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.mockito:mockito-core:5.11.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
	testImplementation 'org.assertj:assertj-core:3.25.3'
	testImplementation 'com.google.code.gson:gson:2.10.1'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.70
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification